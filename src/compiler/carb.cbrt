#include "./syntaxTree/base.hpp"
#include "./syntaxTree/function.hpp"
#include "./syntaxTree/statements.hpp"
#include "./syntaxTree/expressions.hpp"
#include "./syntaxTree/constructs.hpp"
#include "./syntaxTree/literals.hpp"

#include <cstdlib>
#include <string>

%no_externc
%prefix carb_

%header%
#include "./syntaxTree/base.hpp"
%%



%scanner%
%common_class TokenInfo

$:
{
	$._row = $line;
	$._endRow = $endline;
	$._col = $column;
	$._endCol = $endcolumn;
	$._off = $offset;
	$._endOff = $endoffset;
}

: [\ \t\n]+;	/* Skips whitespace. */
ASSIGN: =;
SEMICOL: \;;
COMMA: \,;
PLUS: \+;
MINUS: \-;
ASTERISK: \*;
SLASH: /;
PERCENT: %;
INC: \+\+;
DEC: \-\-;
AND: &;
OR: \|;
XOR: \^;
COMP: ~;
SHR: >>;
SHL: <<;
LAND: &&;
LOR: \|\|;
DENY: !;
EQ: ==;
NE: !=;
GT: >;
LT: <;
GE: >=;
LE: <=;
LPAREN: \(;
RPAREN: \);
LCURLY: \{;
RCURLY: \};
IF: if;
ELSE: else;
MATCH: match;
FOR: for;
LOOP: loop;
WHILE: while;
TRUE: true;
FALSE: false;
BREAK: break;
RETURN: return;
DECIMAL: [0-9]+ { $$ = std::atoi($text); }
ID: [a-zA-Z_][a-zA-Z0-9_]* { $$ = $text; }
STRING: \".*\" { $$ = $text; }

%token ASSIGN SEMICOL COMMA PLUS MINUS ASTERISK SLASH PERCENT INC DEC AND OR XOR COMP SHR SHL LAND
%token LOR DENY EQ NE GT LT GE LE LPAREN RPAREN LCURLY RCURLY DECIMAL ID STRING IF ELSE MATCH FOR
%token LOOP WHILE TRUE FALSE BREAK RETURN

%type DECIMAL: int
%class ID STRING: std::string
/* Not sure what else to put here for now. */
%token_type int


%nt globals global type function_def param_list param_list_tail param statement compound_stmt
%nt statements var_def_stmt expression expression_stmt assignment_expr if_expr for_expr loop_expr
%nt while_expr break_stmt return_stmt expr_maybe primary_expr arg_list arg_list_tail pre_expr
%nt multiplicative_expr additive_expr shift_expr relative_expr absolute_expr bit_and_expr
%nt bit_xor_expr bit_or_expr logic_and_expr logic_or_expr literal boolean


%grammar%
%params std::vector<SyntaxTreeNode*>& output

globals: global globals
	{ output.push_back($0); }
globals: ;

%class global: SyntaxTreeNode*
global: function_def
	{ $$ = $0; }
global: var_def_stmt
	{ $$ = $0; }

%class type: Type
type: ID
	{
		$ = ${0};
		$$ = Type($0);
	}

%class function_def: Function*
function_def: type ID LPAREN param_list RPAREN LCURLY statements RCURLY
	{ $$ = new Function($0, $1, $3, $6); }

%class param_list: Function::ParamList
param_list: param param_list_tail
	{
		Function::ParamList l {$1};
		l.push_back($0);
		$$ = l;
	}
param_list:
	{ $$ = Function::ParamList(); }

%class param_list_tail: Function::ParamList
param_list_tail: COMMA param param_list_tail
	{
		Function::ParamList l {$2};
		l.push_back($1);
		$$ = l;
	}
param_list_tail:
	{ $$ = Function::ParamList(); }

%class param: Parameter
param: type ID
	{ $$ = Parameter($0, $1); }

%class statement: Statement*
statement: compound_stmt
	{ $$ = $0; }
statement: var_def_stmt
	{ $$ = $0; }
statement: expression
	{ $$ = $0; }
statement: expression_stmt
	{ $$ = $0; }
statement: break_stmt
	{ $$ = $0; }
statement: return_stmt
	{ $$ = $0; }

%class compound_stmt: CompoundStmt*
compound_stmt: LCURLY statements RCURLY
	{ $$ = new CompoundStmt($1); }

%class statements: StmtList
statements: statement statements
	{
		StmtList l {$1};
		l.push_back($0);
		$$ = l;
	}
statements:
	{ $$ = StmtList(); }

%class var_def_stmt: VariableDef*
var_def_stmt: type ID ASSIGN expression SEMICOL
	{ $$ = new VariableDef($0, $1, $3); }

%class expression: Expression*
expression: assignment_expr
	{ $$ = $0; }
expression: if_expr
	{ $$ = $0; }
expression: for_expr
	{ $$ = $0; }
expression: loop_expr
	{ $$ = $0; }
expression: while_expr
	{ $$ = $0; }
expression: logic_or_expr
	{ $$ = $0; }

%class expression_stmt: ExprStmt*
expression_stmt: expression SEMICOL
	{ $$ = new ExprStmt($0); }

%class assignment_expr: AssignmentExpr*
assignment_expr: ID ASSIGN expression
	{$$ = new AssignmentExpr($0, $2); }

%class if_expr: IfExpr*
if_expr: IF LPAREN expression RPAREN statement
	{ $$ = new IfExpr($2, $4, nullptr); }
if_expr: IF LPAREN expression RPAREN statement ELSE statement
	{ $$ = new IfExpr($2, $4, $6); }

%class for_expr: ForExpr*
for_expr: FOR LPAREN expr_maybe SEMICOL expr_maybe SEMICOL expr_maybe RPAREN statement
	{ $$ = new ForExpr($2, $4, $6, $8); }

%class loop_expr: LoopExpr*
loop_expr: LOOP statement
	{ $$ = new LoopExpr($1); }

%class while_expr: WhileExpr*
while_expr: WHILE LPAREN expression RPAREN statement
	{ $$ = new WhileExpr($2, $4); }

%class break_stmt: BreakStmt*
break_stmt: BREAK expr_maybe SEMICOL
	{ $$ = new BreakStmt($1); }
break_stmt: BREAK LPAREN DECIMAL RPAREN expr_maybe SEMICOL
	{ $$ = new BreakStmt($4, $2); }

%class return_stmt: ReturnStmt*
return_stmt: RETURN expr_maybe SEMICOL
	{ $$ = new ReturnStmt($1); }

%class expr_maybe: Expression*
expr_maybe: expression
	{ $$ = $0; }
expr_maybe:
	{ $$ = nullptr; }


%class primary_expr: Expression*
primary_expr: LPAREN expression RPAREN
	{ $$ = $1; }
primary_expr: ID LPAREN arg_list RPAREN
	{ $$ = new Invocation($0, $2); }
primary_expr: literal
	{ $$ = $0; }

%class arg_list: Invocation::ArgList
arg_list: expression arg_list_tail
	{
		Invocation::ArgList l {$1};
		l.push_back($0);
		$$ = l;
	}
arg_list:
	{ $$ = Invocation::ArgList(); }

%class arg_list_tail: Invocation::ArgList
arg_list_tail: COMMA expression arg_list_tail
	{
		Invocation::ArgList l {$2};
		l.push_back($1);
		$$ = l;
	}
arg_list_tail:
	{ $$ = Invocation::ArgList(); }

%class pre_expr: Expression*
pre_expr: primary_expr
	{ $$ = static_cast<PreExpr*>($0); }
pre_expr: PLUS primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Pos); }
pre_expr: MINUS primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Neg); }
pre_expr: INC primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Inc); }
pre_expr: DEC primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Dec); }
pre_expr: DENY primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Deny); }
pre_expr: COMP primary_expr
	{ $$ = new PreExpr($1, PreExpr::Ops::Comp); }

%class multiplicative_expr: Expression*
multiplicative_expr: pre_expr
	{ $$ = $0; }
multiplicative_expr: multiplicative_expr ASTERISK pre_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Mul); }
multiplicative_expr: multiplicative_expr SLASH pre_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Div); }
multiplicative_expr: multiplicative_expr PERCENT pre_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Mod); }

%class additive_expr: Expression*
additive_expr: multiplicative_expr
	{ $$ = $0; }
additive_expr: additive_expr PLUS multiplicative_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Add); }
additive_expr: additive_expr MINUS multiplicative_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Sub); }

%class shift_expr: Expression*
shift_expr: additive_expr
	{ $$ = $0; }
shift_expr: shift_expr SHL additive_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::LShift); }
shift_expr: shift_expr SHR additive_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::RShift); }

%class relative_expr: Expression*
relative_expr: shift_expr
	{ $$ = $0; }
relative_expr: relative_expr LT shift_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Less); }
relative_expr: relative_expr LE shift_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::LessEq); }
relative_expr: relative_expr GT shift_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::More); }
relative_expr: relative_expr GE shift_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::MoreEq); }

%class absolute_expr: Expression*
absolute_expr: relative_expr
	{ $$ = $0; }
absolute_expr: absolute_expr EQ relative_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Eq); }
absolute_expr: absolute_expr NE relative_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::Neq); }

%class bit_and_expr: Expression*
bit_and_expr: absolute_expr
	{ $$ = $0; }
bit_and_expr: bit_and_expr AND absolute_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::AND); }

%class bit_xor_expr: Expression*
bit_xor_expr: bit_and_expr
	{ $$ = $0; }
bit_xor_expr: bit_xor_expr XOR bit_and_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::XOR); }

%class bit_or_expr: Expression*
bit_or_expr: bit_xor_expr
	{ $$ = $0; }
bit_or_expr: bit_or_expr OR bit_xor_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::OR); }

%class logic_and_expr: Expression*
logic_and_expr: bit_or_expr
	{ $$ = $0; }
logic_and_expr: logic_and_expr LAND bit_or_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::LAND); }

%class logic_or_expr: Expression*
logic_or_expr: logic_and_expr
	{ $$ = $0; }
logic_or_expr: logic_or_expr LOR logic_and_expr
	{ $$ = new BinaryExpr($0, $2, BinaryExpr::Ops::LOR); }


%class literal: Literal*
literal: ID
	{
		Identifier* id = new Identifier($0);
		id->SetSymbolInfo(${0});
		$$ = id;
	}
literal: DECIMAL
	{
		IntLiteral* lit = new IntLiteral($0);
		lit->SetSymbolInfo(${0});
		$$ = lit;
	}
literal: boolean
	{
		BoolLiteral* lit = new BoolLiteral($0);
		lit->SetSymbolInfo(${0});
		$$ = lit;
	}
literal: STRING
	{
		StrLiteral* lit = new StrLiteral($0);
		lit->SetSymbolInfo(${0});
		$$ = lit;
	}

%type boolean: bool
boolean: TRUE
	{
		$ = ${0};
		$$ = true;
	}
boolean: FALSE
	{
		$ = ${0};
		$$ = false;
	}



%prefer additive_expr: additive_expr * MINUS multiplicative_expr
%over shift_expr: additive_expr *

%prefer additive_expr: additive_expr * PLUS multiplicative_expr
%over shift_expr: additive_expr *

%prefer shift_expr: shift_expr SHR additive_expr *
%over additive_expr: additive_expr * MINUS multiplicative_expr

%prefer shift_expr: shift_expr SHR additive_expr *
%over additive_expr: additive_expr * PLUS multiplicative_expr

%prefer shift_expr: shift_expr SHL additive_expr *
%over additive_expr: additive_expr * MINUS multiplicative_expr

%prefer shift_expr: shift_expr SHL additive_expr *
%over additive_expr: additive_expr * PLUS multiplicative_expr

%prefer primary_expr: ID * LPAREN arg_list RPAREN
%over literal: ID *

%prefer expression_stmt: expression * SEMICOL
%over statement: expression *

%prefer break_stmt: BREAK LPAREN DECIMAL * RPAREN expr_maybe SEMICOL
%over literal: DECIMAL *

%prefer type: ID *
%over literal: ID *
